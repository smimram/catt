coh comp
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
    :
    (x -> z)
let comp f g = comp _ _ f _ g
let comp1 = comp

coh assoc
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
    (w : *) (h : z -> w)
    :
    comp (comp f g) h -> comp f (comp g h)
let assoc f g h = assoc _ _ f _ g _ h

coh id (x : *) : (x -> x)
set unsafe-evars = true
let id = id _
set unsafe-evars = false
let id1 = id

coh unit1l
    (x : *)
    (y : *) (f : x -> y)
    :
    comp id f -> f
let unit1l f = unit1l _ _ f

coh unit1l'
    (x : *)
    (y : *) (f : x -> y)
    :
    f -> comp id f
let unit1l' f = unit1l' _ _ f

coh unit1r
    (x : *)
    (y : *) (f : x -> y)
    :
    comp f id -> f
let unit1r f = unit1r _ _ f

coh unit1r'
    (x : *)
    (y : *) (f : x -> y)
    :
    f -> comp f id
let unit1r' f = unit1r' _ _ f

coh id2
    (x : *)
    (y : *) (f : x -> y)
    :
    (f -> f)
let id2 f = id2 _ _ f

coh vcomp
    (x : *)
    (y : *) (f : x -> y)
    (g : x -> y) (a : f -> g)
    (h : x -> y) (b : g -> h)
    :
    f -> h
let vcomp a b = vcomp _ _ _ _ a _ b
let comp2 = vcomp

coh comp12
    (x : *)
    (y : *) (f : x -> y)
    (z : *) (g : y -> z)
    (g' : y -> z) (a : g -> g')
    :
    comp f g -> comp f g'
let comp12 f a = comp12 _ _ f _ _ _ a

coh comp21
    (x : *)
    (y : *) (f : x -> y)
    (f' : x -> y) (a : f -> f')
    (z : *) (g : y -> z)
    :
    comp f g -> comp f' g
let comp21 a g = comp21 _ _ _ _ a _ g

coh unit12l
    (x : *)
    (y : *) (f : x -> y)
    (g : x -> y) (a : f -> g)
    :
    comp2 (unit1l' f) (comp2 (comp12 id a) (unit1l g)) -> a
let unit12l a = unit12l _ _ _ _ a

coh unit12l'
    (x : *)
    (y : *) (f : x -> y)
    (g : x -> y) (a : f -> g)
    :
    a -> comp2 (unit1l' f) (comp2 (comp12 id a) (unit1l g))
let unit12l' a = unit12l' _ _ _ _ a

coh unit21r
    (x : *)
    (y : *) (f : x -> y)
    (g : x -> y) (a : f -> g)
    :
    comp2 (unit1r' f) (comp2 (comp21 a id) (unit1r g)) -> a
let unit21 r a = unit21r _ _ _ _ a

coh unit21r'
    (x : *)
    (y : *) (f : x -> y)
    (g : x -> y) (a : f -> g)
    :
    a -> comp2 (unit1r' f) (comp2 (comp21 a id) (unit1r g))
let unit21r' a = unit21r' _ _ _ _ a

coh ichg2
    (x : *)
    (y : *) (f : x -> y)
    (f' : x -> y) (a : f -> f')
    (z : *) (g : y -> z)
    (g' : y -> z) (b : g -> g')
    :
    comp2 (comp21 a g) (comp12 f' b) -> comp2 (comp12 f b) (comp21 a g')

coh comp3
    (x : *)
    (y : *) (f : x -> y)
    (g : x -> y) (a : f -> g)
    (b : f -> g) (F : a -> b)
    (c : f -> g) (G : b -> c)
    :
    a -> c
let comp3 F G = comp3 _ _ _ _ _ _ F _ G

coh comp32
    (x : *)
    (y : *) (f : x -> y)
    (g : x -> y) (a : f -> g)
    (b : f -> g) (F : a -> b)
    (h : x -> y) (c : g -> h)
    :
    comp2 a c -> comp2 b c
let comp32 F a = comp32 _ _ _ _ _ _ F _ a

coh comp23
    (x : *)
    (y : *) (f : x -> y)
    (g : x -> y) (a : f -> g)
    (h : x -> y) (b : g -> h)
    (c : g -> h) (F : b -> c)
    :
    comp2 a b -> comp2 a c
let comp23 a F = comp23 _ _ _ _ a _ _ _ F

let comp3_1 F G = comp3 (comp32 F _) (comp23 _ G)

let eh1 (a : id -> id) (b : id -> id) =
    comp3_1 (unit21r' a) (unit12l' b)

# let eckmann-hilton (a : id -> id) (b : id -> id) =
    # comp2 (comp21 a id) (comp12 id b)




# hyp x : *
# hyp y : *
# hyp z : *
# hyp f : x -> y
# hyp g : y -> z
# eval (comp f g)


# set groupoid = true

# coh inv (x : *) (y : *) (f : x -> y) : y -> x